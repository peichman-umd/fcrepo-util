#!/usr/bin/perl -w
use strict;

use Getopt::Long;
use HTTP::Request;
use JSON;

GetOptions(
    'D=s' => \my %PRINCIPALS,
    'u=s' => \my $CREDENTIALS,
);
my $URI = shift;

die "Usage: $0 -D principal=role,role [-u user:password] URI\n" unless %PRINCIPALS && $URI;

my $ua = LWP::UserAgent::InteractiveAuth->new;

my $json_acl = encode_json(
    { map { $_ => [ split /,/, $PRINCIPALS{$_} ] } keys %PRINCIPALS }
);

my $req = HTTP::Request->new(POST => "$URI/fcr:accessroles");
$req->content_type('application/json');
$req->content($json_acl);
$req->authorization_basic(split /:/, $CREDENTIALS) if $CREDENTIALS;

my $res = $ua->request($req);
print $res->status_line, "\n";

package LWP::UserAgent::InteractiveAuth;

use base qw{LWP::UserAgent};

use Term::ReadKey;

sub get_basic_credentials {
    my ($self, $realm, $uri, $is_proxy) = @_;

    print "Authorization required for $uri (Realm=$realm)\n";

    print "Username: ";
    chomp(my $username = <STDIN>);

    print "Password: ";
    ReadMode('noecho');
    chomp(my $password = <STDIN>);
    ReadMode(0);
    print "\n";

    return ($username, $password);
}
